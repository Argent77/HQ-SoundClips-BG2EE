BACKUP ~weidu_external/backup/HQ_SoundClips_BG2EE~
SUPPORT ~https://forums.beamdog.com/discussion/61849~
VERSION ~1.2~

ALWAYS
  // required because of non-standard BACKUP path
  OUTER_TEXT_SPRINT MOD_FOLDER ~HQ_SoundClips_BG2EE~
  // root folder for mutable data
  OUTER_TEXT_SPRINT WORK_FOLDER ~weidu_external~
END

README ~HQ_SoundClips_BG2EE/readme/readme-%LANGUAGE%.txt~
       ~HQ_SoundClips_BG2EE/readme/readme.txt~

LANGUAGE ~English~
         ~english~
         ~HQ_SoundClips_BG2EE/languages/english/setup.tra~
LANGUAGE ~Polski (tlumaczenie: Cahir)~
         ~polish~
         ~HQ_SoundClips_BG2EE/languages/english/setup.tra~
         ~HQ_SoundClips_BG2EE/languages/polish/setup.tra~


BEGIN @0  // Install high quality soundclips for new BG2EE content
  REQUIRE_PREDICATE (GAME_IS ~bg2ee eet~) @100  // BG2EE or EET required.
  DESIGNATED 0

  OUTER_TEXT_SPRINT TEMP_FOLDER ~%WORK_FOLDER%/temp/%MOD_FOLDER%~

  OUTER_TEXT_SPRINT ~char~ ~[^ %TAB%%WNL%]~
  OUTER_TEXT_SPRINT ~8char~ ~%char%%char%?%char%?%char%?%char%?%char%?%char%?%char%?~
  OUTER_TEXT_SPRINT ~ext~ ~wav~
  OUTER_TEXT_SPRINT ~pattern~ ~^%8char%\.%ext%$~

  OUTER_SET maxTotal      = 50000000
  OUTER_SET currentTotal  = 0
  OUTER_SET currentFile   = 0
  OUTER_SET filesCurrent  = 0
  MKDIR ~%TEMP_FOLDER%~
  ACTION_BASH_FOR ~%MOD_FOLDER%/wav~ ~%pattern%~ BEGIN
    ACTION_IF ((BASH_FOR_SIZE + currentTotal > maxTotal || filesCurrent > 16383) && currentTotal > 0) BEGIN
      ACTION_IF (currentFile < 10) BEGIN
        OUTER_TEXT_SPRINT biffFile ~a7#snd0%currentFile%~
      END ELSE BEGIN
        OUTER_TEXT_SPRINT biffFile ~a7#snd%currentFile%~
      END
      MAKE_BIFF ~%biffFile%~ BEGIN ~%TEMP_FOLDER%~ ~^.*$~ END
      OUTER_SET currentFile += 1
      OUTER_SET currentTotal = 0
      OUTER_SET filesCurrent = 0
      DELETE + ~%TEMP_FOLDER%~
      MKDIR ~%TEMP_FOLDER%~
    END
    ACTION_IF (FILE_EXISTS ~override/%BASH_FOR_FILE%~) BEGIN
      DELETE ~override/%BASH_FOR_FILE%~
    END
    COPY + ~%MOD_FOLDER%/wav/%BASH_FOR_FILE%~ ~%TEMP_FOLDER%/%BASH_FOR_FILE%~
    OUTER_SET currentTotal += BASH_FOR_SIZE
    OUTER_SET filesCurrent += 1
  END

  ACTION_IF (currentTotal > 0) BEGIN
    ACTION_IF (currentFile < 10) BEGIN
      OUTER_TEXT_SPRINT biffFile ~a7#snd0%currentFile%~
    END ELSE BEGIN
      OUTER_TEXT_SPRINT biffFile ~a7#snd%currentFile%~
    END
    MAKE_BIFF ~%biffFile%~ BEGIN ~%TEMP_FOLDER%~ ~^.*$~ END
  END

  DELETE + ~%TEMP_FOLDER%~
